//All the liquid gore stuff goes here.


//Hit puff actor

actor Bolognese_Blood Replaces Blood
{
	Decal BrutalBloodSplat
	Renderstyle Translucent
	+FORCEXYBILLBOARD
	+THRUACTORS
	+CLIENTSIDEONLY
	+NOGRAVITY
	Scale 0.8
	Alpha 0.7
	Mass 999999
	 states
	 {
	  Spawn:
			BSPR A 0
			TNT1 A 0 A_CustomMissile ("SuperGoreMistTiny", 0, 0, random (0, 360), 2, random (0, 90))
			TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
			TNT1 A 0 ACS_NamedExecuteAlways("BD_CheckBloodIntensity")
			TNT1 A 0
			TNT1 A 0 A_PlaySound("misc/xdeath5")
			BSPR A 2
			TNT1 A 0 A_CustomMissile ("WaterBloodCHecker", 7, 0, random (0, 360), 2, random (30, 60))
		Spawn2:
			TNT1 A 0 A_CustomMissile ("BD_FlyingBlood", 7, 0, random (0, 360), 2, random (30, 60))
			TNT1 A 0 A_CustomMissile ("BD_FlyingBloodTrail", 0, 0, random (0, 360), 2, random (20, 90))
			Goto Live	
		Splash:
			BLOD A 0
			TNT1 AAA 0 A_CustomMissile ("Underblood1", 7, 0, random (0, 360), 2, random (30, 150))
			stop
		LotsOfBlood:
		   TNT1 A 0
		   TNT1 AAAA 0 A_CustomMissile ("BloodMistSmall", 0, 0, random (0, 360), 2, random (0, 90))
		   TNT1 AAAAA 0 A_CustomMissile ("BD_FlyingBlood", 7, 0, random (0, 360), 2, random (30, 70))
		   TNT1 A 0 A_CustomMissile ("SuperWallRedBlood", 0, 0, random (0, 360), 2, random (-15, 15))
		   TNT1 AAAA 0 A_CustomMissile ("BD_FlyingBloodTrail", 0, 0, random (0, 360), 2, random (0, 90))
		   Goto Live
		AbsurdBlood:
		   TNT1 A 2
		   TNT1 AAAA 0 A_CustomMissile ("SuperWallRedBlood", 0, 0, random (0, 360), 2, random (-15, 15))
		   TNT1 A 0 A_JumpIfInventory("AnimuGore", 1, "AnimuGore")
		   TNT1 AA 0 A_CustomMissile ("BloodMist", 0, 0, random (0, 360), 2, random (0, 90))
		   TNT1 AAA 0 A_CustomMissile ("BD_FlyingBloodMuchFaster", 7, 0, random (0, 360), 2, random (10, 50))
		   TNT1 A 0 A_CustomMissile ("BD_FlyingBloodMuchFaster", 7, 0, random (0, 360), 2, random (40, 60))
		   TNT1 A 0 A_CustomMissile ("BloodmistSmall", 7, 0, random (0, 360), 2, random (30, 90))
		   TNT1 A 0 A_CustomMissile ("SuperGoreMist", 0, 0, random (0, 360), 2, random (10, 90))
		   TNT1 AAAAA 0 A_CustomMissile ("BD_FlyingBlood", 7, 0, random (0, 360), 2, random (30, 70))
		   TNT1 AAA 0 A_CustomMissile ("BloodmistSpray", 0, 0, random (0, 360), 2, random (20, 90))
		   Goto Live
		 AnimuGore:
		   TNT1 AAA 0 A_CustomMissile ("BD_FlyingBloodMuchFaster", 7, 0, random (0, 360), 2, random (10, 50))
		   TNT1 A 0 A_CustomMissile ("SuperWallRedBlood", 0, 0, random (0, 360), 2, random (-15, 15))
		   TNT1 A 0 A_CustomMissile ("BD_FlyingBloodMuchFaster", 7, 0, random (0, 360), 2, random (40, 60))
		   TNT1 AAAAAAA 0 A_CustomMissile ("BD_FlyingBlood", 7, 0, random (0, 360), 2, random (30, 70))
		   TNT1 AAAAAAAAA 0 A_CustomMissile ("XDeath1", 0, 0, random (0, 360), 2, random (40, 80))
		   TNT1 A 0 A_CustomMissile ("SuperGoreSpawner", 0, 0, random (0, 360), 2, random (10, 90))
		   TNT1 AAAAA 0 A_CustomMissile ("BloodmistSpray", 0, 0, random (0, 360), 2, random (20, 90))
		   BSPR ABCDEFG 1
		   BSPR GGGGG 1 A_FadeOut(0.1)
		   Stop   
		 LowBlood:
		 Live:
			TNT1 A 0 A_Stop
			BSPR ABCDEFGHI 1
			Stop
			
		SpawnPlayer:
			TNT1 A 0
			TNT1 A 0 A_CustomMissile ("SuperGoreMistTiny", 0, 0, random (0, 360), 2, 0)
			TNT1 A 0 A_CustomMissile ("BD_FlyingBlood", 7, 0, random (0, 360), 2, random (30, 60))
			TNT1 A 0 A_CustomMissile ("BD_FlyingBloodTrail", 0, 0, random (0, 360), 2, random (20, 90))
			Goto Live	
	 }
}


//Dummy actor just to replace the bloodpuff made by Hexen axe
actor SawBlood : Bolognese_Blood Replaces AxeBlood
{
-ALLOWPARTICLES 
Speed 0
 states
 {
  Spawn:
        TNT1 A 0
		TNT1 AA 0 A_CustomMissile ("BloodMistSmall", 0, 0, random (0, 360), 2, random (0, 90))
		TNT1 AA 0 A_CustomMissile ("XDeath1", 0, 0, random (0, 360), 2, random (40, 80))
		TNT1 AAA 0 A_CustomMissile ("BD_FlyingBlood", 0, 0, random (0, 360), 2, random (30, 60))
		TNT1 A 0 Radius_Quake (2, 2, 0, 2, 0)
		TNT1 A 0 ACS_NamedExecuteAlways("BD_CheckBloodIntensity")
		TNT1 A 0 A_Stop
		TNT1 A 2
		Goto Spawn2
 }
}


//Dummy actor just to replace the bloodpuff made by projectiles
actor BloodSPlatterReplacer : Bolognese_Blood Replaces BloodSplatter
{
}


//Base flying blood actor. Leaves bloodspots on the floor
ACTOR BD_FlyingBlood 
{
   scale 0.6
   speed 4
   radius 8
   height 1
   Alpha 0.6
   Decal BrutalBlood
   Mass 1
   +MISSILE   
   +CLIENTSIDEONLY
   +NOTELEPORT   
   +NOBLOCKMAP
   +THRUACTORS   
   +FORCEXYBILLBOARD
   +MOVEWITHSECTOR   
   +DONTSPLASH
    States
    {
     Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
			TNT1 A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3", "Live")
	 Spawn1:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-0.4, 0.4)
			Goto live
	
	Spawn2:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-0.45, -0.45)
			Goto live
			
	Spawn3:
			TNT1 A 0
			TNT1 A 0 A_SetScale(0.5, -0.5)
			Goto live		
			
	 Live:
			BLHT BCDEFG 1
			TNT1 A 0 
			BLHT HHHHH 100 A_JumpIf(waterlevel > 1, "Splash")
			Stop	
	
	Splash:
			BLOD A 0
			TNT1 A 0 A_SpawnItem ("Underblood2")
			stop
		
     Death:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("blooddrop2")
			TNT1 A 0 A_CheckFloor("DeathFloor")
			TNT1 A 1
			Stop
  
		DeathFloor:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx ("Bolognese_BloodSpot",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION |  SXF_TRANSFERTRANSLATION,0)
			TNT1 A 0 A_PlaySound("blooddrop2")
		    BL8A ABCDEF 2
			Stop
			
		NoSpawn:
			TNT1 A 0
			Stop
			
    }
}


//Faster version used in some specific cases
ACTOR BD_FlyingBloodMuchFaster: BD_FlyingBlood
{
	 Scale 1.0
	 Speed 8
}


actor BD_FlyingBloodTrail 
{
	scale 0.25
	mass 1
	renderstyle Translucent
	alpha 0.8
	Decal BrutalBloodSplat
	+NOTELEPORT
	+NOBLOCKMAP
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+MISSILE
	+THRUACTORS
	+BOUNCEONWALLS
	+DONTSPLASH
	Gravity 0.4
	Speed 2
	states
	{
	  Spawn:
		  TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
		  BLHT BCDEFG 1
		  BLHT HHHHHHHHH 1 A_FadeOut(0.02)
		  stop
	  
	Death:
		TNT1 A 0
		Stop

	Splash:
        BLOD A 0
		TNT1 A 0 A_SpawnItem ("Underblood2")
        stop

 }
}


actor BD_FlyingBloodFaster: BD_FlyingBloodTrail
{
Speed 4
 states
 {
  Spawn:
	  TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
	  BLOD ABCDEFFFFFF 3
	  stop
 }
}


ACTOR BD_FlyingBloodFake: BD_FlyingBloodTrail 
{
	scale 0.5
	speed 3
	Gravity 0.3
	Renderstyle Translucent
	Alpha 0.95
	+NOCLIP
    Mass 1
    States
    {
     Spawn:
	    TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
		BLOD KKKKKKKKKKKK 1 A_FadeOut(0.05)
        Stop
	Splash:
        BLOD A 0
		TNT1 A 0 A_SpawnItem ("Underblood2")
        stop
	Death:
	    TNT1 A 10
		Stop
    }
}

ACTOR BD_FlyingBloodFakeNoDecal: BD_FlyingBloodFake { decal "none"}



actor Bloodmist : BD_FlyingBlood
{
	+BOUNCEONWALLS
	scale 0.8
	radius 2
	height 2
	speed  8
	Gravity 0.6
	alpha 0.6
	 states
	 {
	Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3", "Live")
	 Spawn1:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-0.8, 0.8)
			Goto live
	
	Spawn2:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-0.9, -0.9)
			Goto live
			
	Spawn3:
			TNT1 A 0
			TNT1 A 0 A_SetScale(0.7, -0.7)
			Goto live		
			
	 Live:
			BLHT ABCDEFG 3
			TNT1 A 0 
			BLHT HHHHH 100 A_JumpIf(waterlevel > 1, "Splash")
			Stop	

	Death:
		 TNT1 A 0
		 Stop
	}
}




actor BloodmistSmall : Bloodmist
{
	scale    0.5
	speed    8
	GRAVITY 1.0
	
	states
	 {
	Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3", "Live")
	 Spawn1:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-0.5, 0.5)
			Goto live
	
	Spawn2:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-0.4, -0.4)
			Goto live
			
	Spawn3:
			TNT1 A 0
			TNT1 A 0 A_SetScale(0.5, -0.6)
			Goto live	
		}	
}

actor BloodmistBig : Bloodmist
{
	scale    1
	speed    5
	GRAVITY 0.4
	
	states
	 {
	Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3", "Live")
	 Spawn1:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-1.1, 1.1)
			Goto live
	
	Spawn2:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-0.9, -0.9)
			Goto live
			
	Spawn3:
			TNT1 A 0
			TNT1 A 0 A_SetScale(1.0, -1.0)
			Goto live	
		}	
}



actor BloodmistExtraBig : Bloodmist
{
	scale    1.5
	speed    5
	GRAVITY 0.4
	
	states
	 {
	Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3", "Live")
	 Spawn1:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-1.6, 1.6)
			Goto live
	
	Spawn2:
			TNT1 A 0
			TNT1 A 0 A_SetScale(-1.5, -1.6)
			Goto live
			
	Spawn3:
			TNT1 A 0
			TNT1 A 0 A_SetScale(1.4, -1.5)
			Goto live	
		}	
}

//More wattery version
actor BloodmistSpray : Bloodmist
{
	scale    1.0
	Gravity 0.4
	 states
		{
		Spawn:
			BSPR ABCDEFGH 2
			BSPR H 200
			Stop
		}
}



/////////////////////// BLOOD SPOTS //////////////////////////

//base bloodspot actor
ACTOR Bolognese_BloodSpot
{
	radius 2
	height 2
	mass 1
	+NOTELEPORT
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+MOVEWITHSECTOR
	+FLOORCLIP
	+DONTSPLASH
	-SOLID
	+THRUACTORS
	Alpha 0.99
	Scale 0.5
	Decal BrutalBloodSplat
	states
	 {
	 Spawn:
		 TNT1 A 0
		 TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
		 TNT1 A 1
		 
		 TNT1 A 0 A_CheckFloor("SpawnNormally")
		 Goto Splash
	  
	SpawnNormally:
	NULL A 1
	NULL A 0 A_Jump(255, "Spawn1", "Spawn2", "Spawn3", "Spawn4", "Spawn5", "Spawn6", "spawn7", "spawn8", "spawn9", "live")

	Spawn1:
	NULL A 0 A_SetScale(0.58, 0.58)
	Goto Live

	Spawn2:
	NULL A 0 A_SetScale(0.56, 0.56)
	Goto Live

	Spawn3:
	NULL A 0 A_SetScale(0.54, 0.54)
	Goto Live

	Spawn4:
	NULL A 0 A_SetScale(0.52, 0.52)
	Goto Live

	Spawn5:
	NULL A 0 A_SetScale(0.48, 0.48)
	Goto Live

	Spawn6:
	NULL A 0 A_SetScale(0.46, 0.46)
	Goto Live

	Spawn7:
	NULL A 0 A_SetScale(0.44, 0.44)
	Goto Live

	Spawn8:
	NULL A 0 A_SetScale(0.42, 0.42)
	Goto Live

	Spawn9:
	NULL A 0 A_SetScale(0.40, 0.40)
	Goto Live

	Live:
	TNT1 A 1
	TNT1 A 0 A_QueueCorpse
	TNT1 A 0 A_Jump(255,  "SpawnAFrameOnly", "SpawnBFrameOnly", "SpawnCFrameOnly")

	SpawnAFrameOnly:
			BPDL A -1
			Loop
		  
	SpawnBFrameOnly:
			BPDL B -1
			Loop
		  
	SpawnCFrameOnly:
			BPDL C -1
			Loop
		  
		Splash:
			BLOD A 0
			stop
				
		Death:
			TNT1 A 0
			TNT1 A 0 A_NoBlocking
			Stop
			
		IsOverLiquid:
			BPDL E 1 A_FadeOut(0.9)
			BPDL EEEEEEEEEEEEEEEEEEEE 4 A_FadeIn(0.05)
			BPDL E 400
			BPDL EEEEEEEEEEEEEEEEEEEE 4 A_FadeOut(0.05)
			Stop
	}
}

//Bigger version for gibs
Actor BDLargeBloodSpot: Bolognese_BloodSpot {Mass 1}


//a special flying blood actor that spawns muddy water pools over liquids
ACTOR WaterBloodCHecker
{
    Speed 4
    Scale 1.2
	Radius 1
	+MISSILE
	+EXPLODEONWATER
	+CLIENTSIDEONLY
	+DOOMBOUNCE
	BounceFactor 0.01
	Height 1
    SeeSound "none"
    DeathSound "none"
	Decal none
	States
    {
    Spawn:
	    TNT1 A 5
        Loop
    Death:
        TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("BD_CheckIfOverLiquid")
		TNT1 A 5
		TNT1 A 0 A_JumpIfInventory("IsOverWater", 1, "IsOverLiquid")
		TNT1 A 0 A_JumpIfInventory("IsOverSlime", 1, "IsOverLiquid")
		TNT1 A 0 A_JumpIfInventory("IsOverNukage", 1, "IsOverLiquid")
        TNT1 A 10
        Stop
        
	IsOverLiquid:
		TNT1 A 1
		TNT1 A 0 A_CheckFloor("SpawnOnWater")
		TNT1 A 2
		Stop
		
	SpawnOnWater:
		TNT1 A 1
		TNT1 A 0 A_SpawnItem ("WaterBloodSpot", 5)
		Stop
    }
}

//muddy water bloodspot actor
ACTOR WaterBloodSpot
{
+CLIENTSIDEONLY
+THRUACTORS
Radius 0
Height 0
Renderstyle Shaded
StencilColor "55 00 00"
Alpha 0.2
 states
 {
	Spawn:
		BPDL E 1 A_FadeOut(0.9)
		BPDL EEEEEEEEEEEEEEEEEEEE 4 A_FadeIn(0.02)
		BPDL E 100
		BPDL EEEEEEEEEEEEEEEEEEEE 4 A_FadeOut(0.02)
		Stop
 }
}

//bigger bloodsplat 
actor GiantBloodSpot: Bolognese_BloodSpot
{
 Scale 1.1
 States
 {
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("BD_CheckIfOverLiquid")//Check Floortype
		TNT1 A 3
		TNT1 A 0 A_JumpIfInventory("IsOverSlimy", 1, "Disappear")
		TNT1 A 0 A_JumpIfInventory("IsOverWater", 1, "Disappear")
		TNT1 A 0 A_JumpIfInventory("IsOverNukage", 1, "Disappear")
		TNT1 A 0 A_JumpIfInventory("IsOverLava", 1, "Disappear")
		TNT1 A 0 A_QueueCorpse
		Goto SpawnAFrameOnly
	
	Disappear:
		TNT1 A 0
		Stop
	}
}


actor MinuscleBloodSpot: Bolognese_BloodSpot {Scale 0.25}
actor BigBloodSpot: GiantBloodSpot { Scale 0.7}
actor MediumBloodSpot: GiantBloodSpot { Scale 0.4}
actor MediumBloodSpot2: GiantBloodSpot{ Scale 0.4}


//Big version spawned by XDeath animations
ACTOR SplatteredSmall: Bolognese_BloodSpot
{
 states
 {
 Spawn:
	TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
    TNT1 A 0 A_FaceTarget
	TNT1 A 2
    TNT1 A 0 A_JumpIfInventory("IsOverWater", 1, "Splash")
	TNT1 A 0 A_JumpIfInventory("IsOverSlime", 1, "Splash")
	TNT1 A 0 A_JumpIfInventory("IsOverlava", 1, "Splash")
	BPDL A -1
	Stop
 }
}

//Big version spawned by XDeath animations of bigger enemies
ACTOR SplatteredLarge: Bolognese_BloodSpot
{
 states
 {
 Spawn:
   TNT1 A 0
   TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
   TNT1 A 0 A_CheckFloor("Live")
   Goto Splash
   
  Live: 
    TNT1 A 0
    TNT1 A 0 ACS_NamedExecuteAlways("BD_CheckIfOverLiquid")//Check if is under a liquid
	TNT1 A 4
    TNT1 A 0 A_JumpIfInventory("IsOverWater", 1, "WaterBloodSpot")
	TNT1 A 0 A_JumpIfInventory("IsOverSlime", 1, "WaterBloodSpot")
	TNT1 A 0 A_JumpIfInventory("IsOverlava", 1, "WaterBloodSpot")
	TNT1 A 0 A_QueueCorpse
	BPDL A -1
	Stop
	
 WaterBloodSpot:
	TNT1 A 0
	TNT1 A 0 A_SpawnItem("WaterBloodSpot")
	Stop
 }
}



//A blood pool spawned by regular enemy deaths.
actor GrowingBloodPool: Bolognese_BloodSpot
{
	var int user_bloodpoolsize;
	radius 1
	height 1
	mass 1
	Health 600
	RenderStyle Normal
	Alpha 0.99
	+NOTELEPORT
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+MOVEWITHSECTOR
	+FLOORCLIP
	+MISSILE
	+DONTSPLASH
	Scale 0.3
	Decal BrutalBloodSplat
	 states
		 {
		 Spawn:
		  TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
		  TNT5 A 1
		  
		  TNT5 A 2 A_QueueCorpse
		  TNT5 A 1 A_SetScale(0.33, 0.33)
		  TNT5 A 1 A_SetScale(0.36, 0.36)
		  TNT5 A 1 A_SetScale(0.39, 0.39)
		  TNT5 A 1 A_SetScale(0.42, 0.42)
		  TNT5 A 1 A_SetScale(0.45, 0.45)
		  TNT5 A 1 A_SetScale(0.48, 0.48)
		  TNT5 A 1 A_SetScale(0.50, 0.50)
		  
		  TNT5 A 2 A_SetScale(0.53, 0.53)
		  TNT5 A 2 A_SetScale(0.56, 0.56)
		  TNT5 A 2 A_SetScale(0.59, 0.59)
		  TNT5 A 2 A_SetScale(0.62, 0.62)
		  TNT5 A 2 A_SetScale(0.65, 0.65)
		  TNT5 A 2 A_SetScale(0.68, 0.68)
		  TNT5 A 2 A_SetScale(0.70, 0.70)
		  
		  
		  TNT5 A 3 A_SetScale(0.73, 0.73)
		  TNT5 A 3 A_SetScale(0.76, 0.76)
		  TNT5 A 3 A_SetScale(0.79, 0.79)
		  TNT5 A 3 A_SetScale(0.82, 0.82)
		  TNT5 A 3 A_SetScale(0.85, 0.85)
		  TNT5 A 3 A_SetScale(0.88, 0.88)
		  TNT5 A 3 A_SetScale(0.90, 0.90)
		  
		  TNT5 A 3 A_SetScale(0.93, 0.93)
		  TNT5 A 3 A_SetScale(0.96, 0.96)
		  TNT5 A 3 A_SetScale(0.99, 0.99)
		  TNT5 A 3 A_SetScale(1.02, 1.02)
		  TNT5 A 3 A_SetScale(1.05, 1.05)
		  TNT5 A 3 A_SetScale(1.08, 1.08)
		  TNT5 A 3 A_SetScale(1.11, 1.11)
		  
		 Live:
		  TNT5 A 900//duration
		  TNT1 A 0 A_JumpIfInventory("lowgraphicsmode", 1, "Splash")
		  TNT5 A -1
		  Stop
		Splash:
		  BLOD A 0
		  stop
	 }
}




//A decorative kind of flying blood spawned by arterial blood
ACTOR BD_LiquidBlood: BD_FlyingBlood 
{
 speed 3
 scale 0.2
 gravity 0.6
 decal "BrutalBloodSmearSmall"
    States
    {
     Spawn:
	 TNT1 A 0
	 TNT1 A 0 A_CustomMissile ("BD_FlyingBloodTrail", 10, 0, random (0, 360), 2, random (0, 90))
     BLOD AAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItem("BD_LiquidBloodTrail")
     Stop
	 Death:
  	   TNT1 A 0 A_PlaySound("blooddrop2")
	   BL8A ABCDEF 2
	   TNT1 A 0
       Stop
    }
}

//Faster version, leaves no decals. Used to prevent decal overflow
ACTOR BD_LiquidBlood2: BD_LiquidBlood 
{
 speed 4
 scale 0.4
 gravity 0.6
 decal "None"
}

//Very fast version
ACTOR BD_LiquidBloodFast: BD_LiquidBlood2
{
 speed 8
}

//This version leaves a bloodspot
ACTOR BD_LiquidBlood3: BD_FlyingBlood 
{
Speed 3
    States
    {
	 Death:
  	   TNT1 A 0 A_PlaySound("blooddrop2")
	   TNT1 A 0 A_CheckFloor("SpawnFloor")
	   TNT1 A 4
	   Stop
	  
	 SpawnFloor:
	   tnt1 a 0 A_SPawnItem("Bolognese_BloodSpot")
	   BL8A ABCDEF 2
	   TNT1 A 0
       Stop
    }
}


//Decorative trails
ACTOR BD_LiquidBloodTrail: BD_FlyingBlood 
{
 scale 0.4
 gravity 0.4
    States
    {
     Spawn:
	  TNT1 A 0
      BRTL ABC 2
	  Stop
	 Death:
	  TNT1 A 0
      Stop
    }
}

//An actor that will spawn multiple arterial blood actors
ACTOR ArterialBloodSpray
{
 Speed 0
 +CLIENTSIDEONLY
 +NOINTERACTION
    States
    {
     Spawn:
	 TNT1 A 0
	 TNT1 A 0 A_stop
		TNT1 AAAA 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 6
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 6
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 6
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 6
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 9
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 9
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 9
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 9
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 12
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 12
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 16
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
		TNT1 A 19
		TNT1 A 2 A_CustomMissile ("BD_LiquidBlood2", 0, 0, 0, 2, random (0, 60))
       Stop
    }
}


/////////////////// CEILING BLOOD //////////////////////////

//Launches bloodspots on the ceiling
ACTOR CeilBloodLauncher: BD_FLyingBlood
{
    +THRUACTORS
	-DOOMBOUNCE
	-SKYEXPLODE
	+BOUNCEONWALLS
    Speed 15
    Gravity 0.0
	Radius 1
	Height 1
	Decal BrutalBloodSplat
    States
		{
		 Spawn:
		    TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
			TNT1 A 3
			Stop
		Splash:
			BLOD A 0
			stop
		 Death:
			TNT1 A 0
			TNT1 A 0 A_CheckCeiling("IsOnCeil")
			TNT1 A 0
			stop
			
		 IsOnCeil:
			TNT1 A 0
			TNT1 A 1 A_SpawnItem("CeilBloodSpot",0,0,0,1)
			Stop
		 Melee:
		 Xdeath:
			TNT1 A 1
			Stop
		}
}

//Longer version (for extreme deaths)
ACTOR CeilBloodLauncherLong: CeilBloodLauncher
{
    States
    {
		 Spawn:
			TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
			TNT1 A 12
			Stop
    }
}



// The Ceil Blood Spot
actor CeilBloodSpot
{
	+MISSILE
	+CEILINGHUGGER
	-SKYEXPLODE
	+THRUACTORS
	Height 1
	Radius 2
	+DontSplash
	Mass 0
	Scale 0.5
	Gravity 0.0
	Decal BrutalBloodSplat
	Renderstyle Normal
	Alpha 0.9
	states
	{
		Spawn:
		  BPDL A 1
		  TNT1 A 0 A_QueueCorpse
		  TNT1 A 0 A_SpawnItem ("DripingBloodLeavesSmallPool")
		  TNT1 A 0 A_SpawnItem ("CeilBloodTinyBloodSpawner")
		Live:	
		  BPDL A 5 ThrustThingZ(0, 20, 0, 1)
		  Loop
			}
}

//Bigger version of a ceil blood spot, usually left by sticky gibs.
actor CeilBloodSpotLarge: CeilBloodSpot
{
	states
	{
		Spawn:
			BPDL A 1
			TNT1 A 0 A_QueueCorpse
			TNT1 A 0 A_SpawnItem ("DripingBloodLeavesSmallPool")
			TNT1 A 0 A_SpawnItem ("CeilBloodLargeBloodSpawner")
		Live:	
			BPDL A 5 ThrustThingZ(0, 20, 0, 1)
			Loop
		Splash:
			BLOD A 0
			stop
			}
}

//These are the actors that spawns blood drips. They are separate from the bloodspots so the sticky gibs can spawn them separately.

//Small blood drips spawner, usually caused by regular blood touching the ceiling.
ACTOR CeilBloodTinyBloodSpawner: BD_FlyingBlood
{
	+NOGRAVITY
	+THRUACTORS
	+NOCLIP
	 States
		{
		 Spawn:
			  TNT1 A 0
			  TNT1 A 1 A_SpawnItemEx ("DripingBloodLeavesSmallPool", random (-5, 5), random (-5, 5), 0, 0, 0, -5)
			  TNT1 AAAAAAAAAAAAAA 5 A_SpawnItemEx ("BloodDripingFromCeiling", random (-5, 5), random (-5, 5), 0, 0, 0, -5)
			  TNT1 AAAAAAAAAAAAAA 10 A_SpawnItemEx ("BloodDripingFromCeiling", random (-5, 5), random (-5, 5), 0, 0, 0, -5)
			  TNT1 AAAAAAAAAAAAAA 16 A_SpawnItemEx ("BloodDripingFromCeiling", random (-5, 5), random (-5, 5), 0, 0, 0, -5)
			  TNT1 AAAAAAAAAAAAAA 24 A_SpawnItem ("BloodDripingFromCeiling")
			  stop
		}
}

//Large blood drips spawner, usually caused by huge chunks of meat getting stuck on the ceiling.
ACTOR CeilBloodLargeBloodSpawner: BD_FlyingBlood
{
	+NOGRAVITY
	+THRUACTORS
	+NOCLIP
	Scale 0.5
	 States
		{
		 Spawn:
			  TNT1 A 0
			  TNT1 A 1 A_SpawnItemEx ("DripingBloodLeavesSmallPool", random (-5, 5), random (-5, 5), 0, 0, 0, -5)
			  TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 3 A_SpawnItemEx ("BloodDripingFromCeilingBig", random (-5, 5), random (-5, 5), 0, 0, 0, -5)
			  TNT1 AAAAAAAAAAAAAA 10 A_SpawnItemEx ("BloodDripingFromCeilingBig", random (-5, 5), random (-5, 5), 0, 0, 0, -5)
			  TNT1 AAAAAAAAAAAAAA 15 A_SpawnItemEx ("BloodDripingFromCeiling", random (-5, 5), random (-5, 5), 0, 0, 0, -5)
			  TNT1 AAAAAAAAAAAAAA 20 A_SpawnItemEx ("BloodDripingFromCeiling", random (-5, 5), random (-5, 5), 0, 0, 0, -5)
			  stop 
		}
}

//This is the actual blood driping from the ceiling actor
ACTOR BloodDripingFromCeiling: BD_FlyingBlood
{
	-NOGRAVITY
	-FORCEXYBILLBOARD
	+FORCEYBILLBOARD
	Gravity 1.0
	Radius 4
	Height 1
     States
		{
		 Spawn:
		   TNT1 A 0
		   TNT1 A 0 A_Jump(230, "NoSpawn")
		   TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
		   BLUD Z 500
		   loop
		   
		  Death:
			 TNT1 A 0
			 TNT1 A 0 A_PlaySound("blooddrop2")
			 TNT1 A 0 A_Stop
			 BL8A ABCDEF 2
			 stop 
		}
}

//This is a bigger and denser version of the blood drip, droped by sticky gibs
ACTOR BloodDripingFromCeilingBig: BloodDripingFromCeiling
{
	XScale 0.4
	YScale 1.0
	Gravity 0.8
    States
    {
     Spawn:
       TNT1 A 0
	   TNT1 A 0 A_Jump(160, "NoSpawn")
       TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
       BLUD Z 500
       loop
	 Death:
		 TNT1 A 0
		 TNT1 A 0 A_PlaySound("blooddrop2")
		 TNT1 A 0 A_Stop
		 TNT1 A 0 A_SetScale(0.5, 0.5)
		 BL8A ABCDEF 2
		 stop 
    }
}

//This is the first drip spawned by the drip spawners, which leaves a small blood pool
ACTOR DripingBloodLeavesPool: BloodDripingFromCeiling
{
-FORCEXYBILLBOARD
+FORCEYBILLBOARD

 scale 0.3
    States
    {
     Spawn:
       TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
        BLUD Z 4
        loop
	Splash:
        BLOD A 0
        stop
     Death:
	    TNT1 A 0
		TNT1 A 0 A_SpawnItem("MediumBloodSpot")
		TNT1 A 0 A_PlaySound("blooddrop2")
        BL8A ABCDEF 2
        stop
    }
}

//Just like above, but leaves a smaller pool
ACTOR DripingBloodLeavesSmallPool: DripingBloodLeavesPool
{
-FORCEXYBILLBOARD
+FORCEYBILLBOARD
+DONTSPLASH
States
{
     Death:
	    TNT1 A 0
		TNT1 A 0 A_SpawnItem("MinuscleBloodSpot")
		TNT1 A 0 A_PlaySound("blooddrop2")
        BL8A ABCDEF 2
        stop
    }
}

//An invisible projectile only used to spawn bloodsplats on the walls, usually spawned by extreme deaths.
ACTOR WallRedBlood
{
	scale 0.6
	speed 7
	health 1
	radius 8
	height 1
	Gravity 0.7
	damage 0
	DamageType Blood
	Decal BrutalBloodSmearer
   +MISSILE
   +CLIENTSIDEONLY
   +NOTELEPORT
   +NOBLOCKMAP
   +THRUACTORS
    States
    {
		Spawn:
		   TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
			TNT1 ABCD 4
			loop
		Splash:
			BLOD A 0
			stop
		 Death:
			TNT1 A 3
			stop
    }
}

//This one spawns an even bigger decal
ACTOR SuperWallRedBlood: WallRedBlood
{
 speed 32
 Gravity 0.1
 Decal BrutalBlood
     States
		{
		 Spawn:
		   TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
			TNT1 ABCD 1
			Stop
		}
}


//This one spawns a ridiculously bigger decal, used by BFG extreme deaths
ACTOR GiantWallRedBlood: WallRedBlood
{
 speed 32
 Gravity 0.1
 Decal BrutalBloodGiant
     States
		{
		 Spawn:
		   TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
		   TNT1 ABCD 1
		   Stop
		
		}
}

//Spawns giant blood decals all around the area. Used in Brutal Doom's BFG deaths.
Actor BloodSplinter
{
	+THRUACTORS
	+CLIENTSIDEONLY
	States
		{
			 Spawn:
			   TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
			   TNT1 A 0 A_CustomMissile ("GiantWallRedBlood", 40, 0, 0, 2, random (0, 35))
			   TNT1 A 0 A_CustomMissile ("GiantWallRedBlood", 40, 0, 45, 2, random (0, 35))
			   TNT1 A 0 A_CustomMissile ("GiantWallRedBlood", 40, 0, 90, 2, random (0, 35))
			   TNT1 A 0 A_CustomMissile ("GiantWallRedBlood", 40, 0, 135, 2, random (0, 35))
			   TNT1 A 0 A_CustomMissile ("GiantWallRedBlood", 40, 0, 180, 2, random (0, 35))
			   TNT1 A 0 A_CustomMissile ("GiantWallRedBlood", 40, 0, 225, 2, random (0, 35))
			   TNT1 A 0 A_CustomMissile ("GiantWallRedBlood", 40, 0, 270, 2, random (0, 35))
			   TNT1 A 0 A_CustomMissile ("GiantWallRedBlood", 40, 0, 315, 2, random (0, 35))
			   Stop
			
			Splash:
				BLOD A 0
				stop
		}
}


//Spawned by BD's kicked heads  to spawn blood decals and make the head bounce sound.
ACTOR ShortWallRedBlood: WallRedBlood
{
 speed 2
 Gravity 1
 Radius 24
 Height 12
     States
    {
     Spawn:
       TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
        TNT1 A 3
        Stop
	Death:
	    TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BD_FlyingBloodFakeNoDecal", 0, 0, random (0, 360), 2, random (0, 90))
		TNT1 A 0 A_SpawnItem("Bolognese_BloodSpot",0,0,0,1)
		TNT1 A 0 A_PlaySound("HEDBON")
	    TNT1 A 20
		Stop
	}
}





//Blood actors spawned underwater
ACTOR Underblood1
{
	scale 1.4
	speed 1
	health 1
	radius 8
	height 1
	Gravity 0.9
	Renderstyle Translucent
	Alpha 0.2
	Decal BrutalBloodSplat
	+MISSILE
	+CLIENTSIDEONLY
	+NOTELEPORT
	+NOBLOCKMAP
	+FORCEXYBILLBOARD
	-DONTSPLASH
	+THRUACTORS
	Mass 1
		States
		{
		 Spawn:
		  BSP4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 A_JumpIf(waterlevel < 1, "Splash")
		  Stop
		 
		Splash:
			BLOD A 0
			stop
		 Death:
			TNT1 A 0
			Stop
		}
}

//Shorter version
ACTOR Underblood2: Underblood1
{
 scale 0.7
 speed 0
    States
    {
     Spawn:
	 BSP4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 A_JumpIf(waterlevel < 1, "Splash")
	 Stop
	 
	Splash:
        BLOD A 0
        stop
     Death:
  	    TNT1 A 0
        Stop
    }
}
//Bigger version that moves, used for gibbing.
ACTOR Underblood3: Underblood1
{
 scale 2.2
 speed 2
 Alpha 0.3
}


//Big blood mists


actor SuperGore
{
	Decal BloodSplat
	Alpha 0.4
	+FORCEXYBILLBOARD
	+GHOST
	+NOBLOCKMAP
	Gravity 0.01
	+DontSplash
	-EXPLODEONWATER
	-ALLOWPARTICLES 
	+CLIENTSIDEONLY
	-NOGRAVITY
	+THRUACTORS
	+NOCLIP
	Gravity 0.1
	Scale 0.4
	Speed 3
	Alpha 0.6
	 states
	 {
	  Spawn:
		TNT1 A 0
		BLOR EFGHIJK 4
		Stop
	 }
}


Actor SuperGoreMist: SuperGore
{
Scale 0.6
Speed 3
+NOGRAVITY
}


//Used on bullet hit impacts
Actor SuperGoreMistTiny: SuperGore
{
Scale 0.15
Alpha 0.7
Speed 0
	 states
	 {
	  Spawn:
		TNT1 A 0
		BLOR EFGHIJK 2
		Stop
	 }
}

actor BigSuperGore: SuperGore
{
Scale 0.6
Speed 6
}


actor GiantSuperGore: SuperGore
{
Scale 1.2
Speed 8
}


//Spawners for the supergore thing
Actor SuperGoreSpawner
{
	Projectile
	+MISSILE
	+FORCEXYBILLBOARD
	+THRUACTORS
	Decal BloodSuper
	radius 2
	height 2
	speed 15
	states
	{
	Spawn:
		TNT1 A 3
		TNT1 AAA 3 A_CustomMissile("SuperGore", 7, 0, random (0, 360), 2, random (30, 60))
		Stop
	Death:
	XDeath:
		TNT1 A 0
		Stop
	}
}



//Bloody footprints still requires the player to be edited to work.
//Gives the player an inventory item to indicate the feet are smeared.
ACTOR BootSmearerRed: GrowingBloodPool
{
	MONSTER
	-SOLID
	+NOCLIP
	+THRUACTORS
	-COUNTKILL
	+LOOKALLAROUND
	-FRIENDLY
	Radius 0
	Height 0
	 states
	 {
	 Spawn:
		 TNT1 A 0
		 TNT1 A 1 A_Look
		 Goto See
	  
	 See:
		 TNT1 A 1 A_Chase("","")
		 TNT1 A 0 A_Die("LookForPlayer")
	 Death:	
	 Death.LookForPlayer:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 8 A_JumpIfCloser(60, "SmearBootsWithRedBlood")
		Stop
	SmearBootsWithRedBlood:
		//TNT1 A 0 A_GiveToTarget("BootsSmearedWithRedBlood", 8)
		BPDL A 0
		Stop
	}
}

ACTOR RedBloodFootPrintLeft: GrowingBloodPool
{
	Scale 0.09
	+CLIENTSIDEONLY
	Renderstyle Shaded
	StencilColor "55 00 00"
	 states
	 {
	 Spawn:
	 Death:
	 Crash:
		TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
		TNT1 A 0 ACS_NamedExecuteAlways("BD_CheckIfOverLiquid")//Check if is under a liquid
		TNT1 A 4
		
		TNT1 A 0 A_JumpIfInventory("IsOverWater", 1, "Splash")
		TNT1 A 0 A_JumpIfInventory("IsOverSlime", 1, "Splash")
		TNT1 A 0 A_JumpIfInventory("IsOverlava", 1, "Splash")
		TNT1 A 0 A_CheckFloor("ShowUp")
		Goto Splash
	ShowUp:
		TNT1 A 0
		TNT1 A 0 A_QueueCorpse
		BPDL A 200
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode", 1, "Splash")
		BPDL A -1
		Stop
	Splash:
		TNT1 A 0
		Stop	
	 }
}


ACTOR RedBloodFootPrintRight: GrowingBloodPool
{
Scale 0.09
+CLIENTSIDEONLY
Renderstyle Shaded
StencilColor "55 00 00"
 states
 {
	 Spawn:
	 Death:
	 Crash:
		TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
		TNT1 A 0 ACS_NamedExecuteAlways("BD_CheckIfOverLiquid")//Check if is under a liquid
		TNT1 A 4
		
		TNT1 A 0 A_JumpIfInventory("IsOverWater", 1, "Splash")
		TNT1 A 0 A_JumpIfInventory("IsOverSlime", 1, "Splash")
		TNT1 A 0 A_JumpIfInventory("IsOverlava", 1, "Splash")
		TNT1 A 0 A_CheckFloor("ShowUp")
		Goto Splash
		ShowUp:
		TNT1 A 0
		TNT1 A 0 A_QueueCorpse
		BPDL A 200
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode", 1, "Splash")
		BPDL A -1
		Stop
	Splash:
		TNT1 A 0
		Stop
 }
}