



actor FireOnceToken : Inventory {
	Inventory.MaxAmount 1
	+UNDROPPABLE
}



actor PRLD_DBSelected : Inventory { 
	Inventory.MaxAmount 1
	+UNDROPPABLE
}
ACTOR DoubleBarrel : Postal3dWeapon Replaces Shotgun
{
Weapon.AmmoUse1 0
	Weapon.AmmoGive1 2
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "Buckshot"
	Weapon.AmmoType2 "Magazine2"
Scale 0.5
//Inventory.PickupMessage "\c[red]You got the \c[Orange]Double-Barrel Shotgun"
Inventory.PickupMessage "You got the \c[Orange]Double-Barrel Shotgun"
  //Inventory.PickupSound "SCLOS"
  +weapon.NoAutoaim
  +Weapon.NoAutoFire
  
  States
  { 
	Ready:
	TNT1 A 0 A_JumpIfInventory("Magazine2", 1, "Ready2")
	TNT1 A 0 A_GiveInventory("PRLD_EmptyWeapon", 1)
	Ready2:
	TNT1 A 0 A_JumpIfInventory("Kicking",1,"Kick")
	TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
	DBRL A 1 A_WeaponReady (WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
	loop
	
	Select:
	TNT1 A 0 A_JumpIfInventory("Magazine2", 1, 2)
	TNT1 A 0 A_GiveInventory("PRLD_EmptyWeapon", 1)
	TNT1 A 1
	TNT1 AAAAAAAAAAAAAAA 0 A_Raise
	TNT1 A 1 Offset(119, 99)
	TNT1 A 0 A_PlaySound ("WEPSEL",CHAN_AUTO)
	TNT1 A 0 A_Jump(226, 2)
	TNT1 A 0 A_PlaySound("Dude/DB/Select", CHAN_VOICE)
	TNT1 A 0 A_GiveInventory("PRLD_DBSelected", 1)
	DBRL A 1 Offset(119, 99)
	DBRL A 1 Offset(76, 65)
	DBRL A 1 Offset(46, 53)
	DBRL A 1 Offset(21, 39)
	DBRL A 1 Offset(5, 35)
	Goto Ready
	
	Deselect:
	DBRL A 1 Offset(5, 35)
	DBRL A 1 Offset(21, 39)
	DBRL A 1 Offset(46, 53)
	DBRL A 1 Offset(76, 65)
	DBRL A 1 Offset(119, 99)
	TNT1 A 0 A_TakeInventory("PRLD_DBSelected", 1)
	TNT1 A 5
	TNT1 A 0 A_TakeInventory("PRLD_EmptyWeapon", 1)
	TNT1 AAAAAAAAAAAAAAA 0 A_Lower
	Wait
	
	KickFlash:
	DBRL A 16
	Stop
	
	Spawn:
	DOBL A 1
	loop

Reload:
TNT1 A 0 A_JumpIfInventory ("Magazine2",2,"Ready")
Goto Reloading

	AltFire:
	TNT1 A 0 A_JumpIfInventory ("Magazine2",2,"FireBoth")
	goto OutOfAmmo
	FireBoth:
		TNT1 A 0 A_Takeinventory("DoubleMagazine2",1)
	    TNT1 A 0 A_TakeInventory ("Magazine2",2)
		//ShakeYourAss actors make the screen shake, easy way to make weapons feel more punchy
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssQuad", 0, 0, 0, 0)
	    TNT1 A 0 A_Recoil (2)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_FireCustomMissile("P3D_ShotgunTracer",random(-10,10),0,0,0,0,random(-5,5))
		SMGR A 0 A_SpawnItemEx("PRLD_MuzzleFlash3", 31, 4, 38)
		TNT1 A 0 A_setpitch(pitch-2)
		TNT1 A 0 A_PlaySound("weapons/dbshotgun/altfire",CHAN_AUTO)
		//TNT1 A 0 A_PlaySound("weapons/dbshotgun/fire",CHAN_AUTO)
		//TNT1 A 0 A_PlaySound("weapons/dbshotgun/fire",CHAN_AUTO)
		TNT1 AAAAA 0 A_FireProjectile("SmokeSpawner",0,0,5,1)
		TNT1 A 0 A_Jump(226, 2)
		TNT1 A 0 A_PlaySound("Dude/DB/Shooting", CHAN_VOICE)
		DBLB A 2 BRIGHT
		DBLB BCDE 3
		DBLB F 2
		DBLB F 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		goto Ready
		
		
	Fire:
		TNT1 A 0 A_JumpIfInventory ("Magazine2",2,"FireLeft")
		TNT1 A 0 A_JumpIfInventory ("Magazine2",1,"FireRight")
		goto OutOfAmmo
	FireLeft:
		TNT1 A 0 A_Takeinventory("DoubleMagazine2",1)//idk what this is for just keeping it just in case
	    TNT1 A 0 A_TakeInventory ("Magazine2",1)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0)
	    TNT1 A 0 A_Recoil (1)
		TNT1 A 0 A_AlertMonsters
		//TNT1 AAAAAAAAA 0 A_FireCustomMissile("P3D_ShotgunTracer",random(-3,3),0,0,0,0,random(-3,3))
		TNT1 AAAAAAAAA 0 A_FireCustomMissile("P3D_ShotgunTracer",frandom(-3.0,3.0),0,0,0,0,random(-3.0,3.0))
		SMGR A 0 A_SpawnItemEx("PRLD_MuzzleFlash2", 31, 4, 38)
		TNT1 A 0 A_setpitch(pitch-1)
		TNT1 A 0 A_PlaySound("weapons/dbshotgun/fire",CHAN_AUTO)
		TNT1 AAA 0 A_FireProjectile("SmokeSpawner",0,0,5,1)
		TNT1 A 0 A_Jump(226, 2)
		TNT1 A 0 A_PlaySound ("Dude/DB/Shooting", CHAN_VOICE)
		DBLF A 2 Bright
		DBLF C 3
		DBLF D 2
		DBLF D 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		DBLF E 3 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		goto Ready
	
	FireRight:
		TNT1 A 0 A_Takeinventory("DoubleMagazine2",1)
	    TNT1 A 0 A_TakeInventory ("Magazine2",1)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0)
	    TNT1 A 0 A_Recoil (1)
		TNT1 A 0 A_AlertMonsters
		//TNT1 AAAAAAAAA 0 A_FireCustomMissile("P3D_ShotgunTracer",random(-3,3),0,0,0,0,random(-3,3))
		TNT1 AAAAAAAAA 0 A_FireCustomMissile("P3D_ShotgunTracer",frandom(-3.0,3.0),0,0,0,0,frandom(-3.0,3.0))
		SMGR A 0 A_SpawnItemEx("PRLD_MuzzleFlash2", 31, 4, 38)
		TNT1 A 0 A_setpitch(pitch-1)
		TNT1 A 0 A_PlaySound("weapons/dbshotgun/fire",CHAN_AUTO)
		TNT1 AAA 0 A_FireProjectile("SmokeSpawner",0,0,5,1)
		TNT1 A 0 A_Jump(226, 2)
		TNT1 A 0 A_PlaySound ("Dude/DB/Shooting", CHAN_VOICE)
		TNT1 A 0 A_GiveInventory("PRLD_EmptyWeapon", 1)
		DBLF B 2 Bright
		DBLF C 3
		DBLF D 2
		TNT1 A 0 A_TakeInventory ("FireOnceToken",1)
		DBLF D 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		DBLF E 3 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		goto Ready
	
OutOfAmmo:
TNT1 A 0 A_PlaySound ("AKEMPT",CHAN_AUTO)
TNT1 A 0 A_Print ("\c[yellow]Out of ammo",1)
Goto Ready

Reloading:
TNT1 A 0 A_JumpIfInventory ("Magazine2",2,"Ready")
TNT1 A 0 A_JumpIfInventory ("Buckshot",1,"StartReload")
goto Ready
StartReload:
	TNT1 A 0 A_JumpIfInventory ("Magazine2",2,"Ready")
	TNT1 A 0 A_PlaySound ("SOPN",CHAN_AUTO)
	DBLR A 3
	TNT1 AAAAAAAA 0 A_FireProjectile("SmokeSpawner",0,0,5,1)
	TNT1 AA 0 A_FireCustomMissile ("ShotgunCasingSpawner",-4,0,11,-10)
	DBLR BCD 3
	TNT1 A 0 A_PlaySound ("WEPSEL",CHAN_AUTO)
	DBLR EFGHIJ 3
	TNT1 A 0 A_PlaySound ("weapons/db/insert",CHAN_AUTO)
	DBLR KLMNOPQ 3
	Goto Bulletin
ReloadContinue:
	TNT1 A 0 A_TakeInventory("PRLD_EmptyWeapon", 1)
	TNT1 A 0 A_PlaySound ("weapons/db/insert",CHAN_AUTO)
	DBLR RSTUVWX 3
	TNT1 A 0 A_PlaySound ("SCLOS",CHAN_AUTO)
	DBLR Y 2
	DBLR Y 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
	goto Ready

Bulletin:
TNT1 AAAA 0
		TNT1 A 0 A_JumpIfInventory("Magazine2",2,"ReloadContinue")
		TNT1 A 0 A_JumpIfInventory("Buckshot",1,"Bulletin2")
	   goto Ready
		Bulletin2:
        TNT1 AAAAAA 0
		TNT1 A 0 A_Giveinventory("Magazine2",1)
		TNT1 A 0 A_Giveinventory("DoubleMagazine2",1)
		TNT1 A 0 A_Takeinventory("Buckshot",1)
		Goto Bulletin

	ExecuteFlash:
		TNT1 A 0 A_SetPitch(65)
		DBRL A 56
		TNT1 A 0 A_JumpIfInventory ("Magazine2",1,"ExecuteFlashCon1")
		Goto KickFlash2Con
	ExecuteFlashCon1:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory ("Magazine2",2,"ExecuteFlashL")
		TNT1 A 0 A_JumpIfInventory ("Magazine2",1,"ExecuteFlashR")
	ExecuteFlashR:
		TNT1 A 0 A_Takeinventory("DoubleMagazine2",1)//idk what this is for just keeping it just in case
	    TNT1 A 0 A_TakeInventory ("Magazine2",1)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssDouble")
		TNT1 A 0 A_PlaySound("weapons/dbshotgun/fire",CHAN_AUTO)
		TNT1 AAA 0 A_FireProjectile("SmokeSpawner",0,0,5,1)
		TNT1 A 0 A_GiveInventory("ExecutionHealth", 9)
		TNT1 A 0 A_Log("+9 health")
		DBLF B 2 Bright
		DBLF CD 3
		DBLF E 9
		Goto ExecuteFlashCon2
	ExecuteFlashB:
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssQuad")
		TNT1 A 0 A_PlaySound("weapons/dbshotgun/altfire",CHAN_AUTO)
		TNT1 A 0 A_Takeinventory("DoubleMagazine2",1)//idk what this is for just keeping it just in case
	    TNT1 A 0 A_TakeInventory ("Magazine2",1)
		TNT1 AAAAA 0 A_FireProjectile("SmokeSpawner",0,0,5,1)
		TNT1 A 0 A_GiveInventory("ExecutionHealth", 12)
		TNT1 A 0 A_Log("+12 health")
		DBLB A 2 BRIGHT
		DBLB BCDEF 3
		Goto ExecuteFlashCon2
	ExecuteFlashL:
		TNT1 A 0 A_Takeinventory("DoubleMagazine2",1)//idk what this is for just keeping it just in case
	    TNT1 A 0 A_TakeInventory ("Magazine2",1)
		TNT1 A 0 A_Jump(63, "ExecuteFlashB")
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssDouble")
		TNT1 A 0 A_PlaySound("weapons/dbshotgun/fire",CHAN_AUTO)
		TNT1 AAA 0 A_FireProjectile("SmokeSpawner",0,0,5,1)
		TNT1 A 0 A_GiveInventory("ExecutionHealth", 9)
		TNT1 A 0 A_Log("+9 health")
		DBLF A 2 Bright
		DBLF CD 3
		DBLF E 9
	ExecuteFlashCon2:
		TNT1 A 0 A_SetPitch(24)
		DBRL A 1 A_SetPitch(Pitch-7)
		DBRL A 1 A_SetPitch(Pitch-5)
		DBRL A 1 A_SetPitch(Pitch-4)
		DBRL A 1 A_SetPitch(Pitch-3)
		DBRL A 1 A_SetPitch(Pitch-2)
		DBRL A 1 A_SetPitch(Pitch-1)
		DBRL A 1 A_SetPitch(Pitch-1)
		DBRL A 1 A_SetPitch(Pitch-1)//24
		Stop

	KickFlash2:
		DBRL A 1 A_SetPitch(-24)
		DBRL A 1 A_SetPitch(Pitch+1)
		DBRL A 1 A_SetPitch(Pitch+1)
		DBRL A 1 A_SetPitch(Pitch+1)
		DBRL A 1 A_SetPitch(Pitch+2)
		TNT1 A 1 A_SetPitch(Pitch+4)
		TNT1 A 1 A_SetPitch(Pitch+5)
		TNT1 A 1 A_SetPitch(Pitch+8)
		TNT1 A 26 A_SetPitch(65)
	KickFlash2Con:
		TNT1 A 0 A_PlaySound("weapons/melee/kick")
		KICK A 3
		TNT1 A 0 A_PlaySound("weapons/melee/heavy", CHAN_AUTO)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
		KICK B 6
		KICK CD 3
		TNT1 A 5
		
		TNT1 A 0 A_PlaySound("weapons/melee/kick")
		KICK A 3
		TNT1 A 0 A_PlaySound("weapons/melee/heavy", CHAN_AUTO)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
		KICK B 6
		KICK CD 3
		TNT1 A 5
		
		TNT1 A 0 A_PlaySound("weapons/melee/kick")
		KICK A 3
		TNT1 A 0 A_GiveInventory("ExecutionHealth", 4)
		TNT1 A 0 A_Log("+4 health",1)
		TNT1 A 0 A_PlaySound("weapons/melee/heavy", CHAN_AUTO)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
		KICK B 6
		KICK CD 3
		TNT1 A 6
		
		TNT1 A 0 A_SetPitch(24)
		TNT1 A 1 A_SetPitch(Pitch-7)
		TNT1 A 1 A_SetPitch(Pitch-5)
		TNT1 A 1 A_SetPitch(Pitch-4)
		TNT1 A 1 A_SetPitch(Pitch-3)
		TNT1 A 1 A_SetPitch(Pitch-2)
		TNT1 A 1 A_SetPitch(Pitch-1)
		TNT1 A 1 A_SetPitch(Pitch-1)
		TNT1 A 1 A_SetPitch(Pitch-1)//24
		DBRL A 5
		Stop
  }
}
ACTOR Magazine2 : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 2
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 2
   scale 0.2
   Inventory.Icon "SHLSC0"
}

ACTOR DoubleMagazine2 : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 4
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 4
   Inventory.Icon "AKMGC0"
}