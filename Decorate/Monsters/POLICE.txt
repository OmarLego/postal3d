ACTOR PoliceBrownPistolWhiteMale : Postal3dMonsterBase
{
    Health 40
	Radius 20
	Height 56
	Speed 4
	FastSpeed 8
    Mass 100
    PainChance 200
    SeeSound "grunt/sight"
    PainSound "grunt/pain"
    DeathSound ""
    ActiveSound "grunt/active"
    DropItem "Clip"
    Obituary "%o was shot by a Police Officer."
	+FLOORCLIP
	+MISSILEMORE
    Species Hostile
	SCALE 0.60
	States
	{
	Spawn:
		SHRW AA 10 A_Look
		Loop
	See:
		SHRW AAA 2 A_Chase
		SHRW BBB 2 A_Chase
		SHRW CCC 2 A_Chase
		SHRW DDD 2 A_Chase
		Loop
	Missile:
		SHRF AB 7 A_FaceTarget
	FireGun:
		TNT1 A 0 A_PlaySound("hostpis")
		SHRF C 2 BRIGHT A_CustomMissile("ZombieManTracer", 48, 0, random(-2, 2), 1, random(-2, 2))
		SHRF D 12 A_FaceTarget
		TNT1 A 0 A_Jump(96, "FireGun")
		Goto See
	Pain:   
		TNT1 A 0 A_Jump(128, "Pain2")
		SHRP A 3
		SHRP A 4 A_Pain
		Goto See
	Pain2:
		SHRP B 3
		SHRP B 4 A_Pain
		Goto See
	Pain.Kick:
		SHRW A 3
		SHRW A 4 A_Pain
		Goto See
    Death:
    Death.Bullet:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieNormal", "Crawl")
    Death.Kick:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_Jump(256, "DieKick")
    Death.Explosive:
        TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_JumpIf(health > -10, "DieNormal")
        TNT1 A 0 A_Jump(256, "DieX")
    XDeath:
        TNT1 A 0 A_XScream
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Jump(256, "DieX")
    DieNormal:
        SRFD ABCDEFGIJKL 3
		TNT1 A 0 A_SpawnItemEx("GrowingBloodPool")
        SRFD L -1
    DieKick:
        TNT1 A 0 ThrustThingZ(0,10,0,1)
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_Recoil(random(5, 7))
        SRFD ABCDEFGIJKL 3
		TNT1 A 0 A_SpawnItemEx("GrowingBloodPool")
        SRFD K -1
    DieX:
        TNT1 A 0 A_SpawnItemEx("MeatDeath")
		TNT1 A 0 A_SpawnItemEx("GrowingBloodPool")
		Stop
    Raise:
        SRFD LKJIGFEDCBA 3
        Goto See
	Crawl:
        TNT1 A 0 A_SpawnItem("DyingPoliceBrownWhiteMale")
		Stop
	}
}